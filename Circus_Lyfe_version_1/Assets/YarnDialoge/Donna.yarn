title: DonnaIntro
tags: Major, Cutscene
---
<<TeleportPlayerHouse Donna>>
<<TeleportPlayerHouse PlayerAvatar>>
Donna: Hey!
<<Move Donna up 3>>
Donna: You're the new trapeze recruit, right?
->Yes
    Donna: Welcome to the team, glad to have ya on board.
	<<IncreaseTrust PlayerAvatar 1>>
->No
	<<Move Donna left 2>>
    Donna: Huh, coulda sworn I was supposed ta meet them here.
	<<Move Donna right 2>> 
	You wouldn't happen to know where they are, would you?
	...
	Oh, yer just pulling my leg... 
	Well anyways, welcome to the team.
	<<DecreaseTrust PlayerAvatar 1>>
<<Move Donna up 1>>
Donna: What's your name anyway, pal?
Donna: [$PlayerName], huh? Nice. 
I'm Donna, the lead trapeze artist round here. 
Well, technically I'm just 'the trapeze artist,' but since you signed up as my trainee I think I can embellish my title a little bit. 
Anyways, since you're still new to the whole trapeze gig I'm gonna be showing you the ropes. 
Meet me at the trapeze tent, and we'll get started with your first practice session.
<<Move Donna Left 20>>
<<wait 3>>
<<TeleportTrapezeTent Donna>>
<<stop>>
===

title: DonnaBackstory
tags: Major, Cutscene
---

<<TeleportPlayerHouse Donna>>
<<TeleportPlayerHouse PlayerAvatar>>
<<Move Donna up 3>>
Donna: Hey, [$PlayerName], I need to talk to you about something. 
Can you come with me to the trapeze tent?
<<TeleportTrapezeTent PlayerAvatar>>
<<TeleportTrapezeTent Donna>>
Donna: I've been watching your progress on the trapeze, and you've improved quite a fair bit. 
I'd say that you could easily captivate an audience on your own, and with a bit more work you might even be able to give me a run for my money. 
But... that's all we are at the moment, individual acts. 
I've been thinking lately, about when you first introduced yourself. 
I told ya 'welcome to the team,' but I've just been treating you like a student and not as a team member.
I know I need to fix that, but, well... I've been scared. 
->What are you scared of?
    Donna: Ugh, no point in beating round the bush.
->Are you scared of me?
    Donna: Not exactly, but kind of... 
	But it's nothing you did! 
	It's me, 
	You see...
->Scared of being overshadowed?
    Donna: No, not that. 
	After all, doesn't every teacher want their student to surpass them?
	It's, well...
Donna: I'm scared of heights. 
Well, scared of falling honestly. 
Have been since I was a kid. 
Wouldn't even climb a bunk bed cause it was so terrifying. 
->Sounds pretty bad.
    Donna: It was, I barely survived having a classroom on the second floor in school. 
	Thought I'd never be able to handle it. 
	But that's part of why I became so entranced with the trapeze.
->Why did you become a trapeze artist then?
    Donna: It sounds weird, but my phobia is the reason I became one.
Donna: I was watching a circus performance on TV when I first saw someone perform on the trapeze. 
It was absolutely magical. 
The artists were twirling through the air as if gravity couldn't affect them, grabbing the trapeze fearless of falling. 
In fact, they couldn't fall! 
They knew how to catch themselves, they had magic that would keep them from hitting the ground. 
I was convinced of that, sure that if I could do what they did I would never have to fear heights again. 
The next time I was at the park, I managed to drum up the courage to climb a tree. 
I grabbed onto a branch, and started swinging myself. 
Aiming at another nearby branch I jumped, trying to do the pirouette I saw on the TV.
 ->Were you able to make it?
	Donna: Course not, fell just as I feared I would. 
	Broke my leg pretty badly, too.
->That couldn't have gone well. 
	Donna: Hehe, it most certainly did not. 
	Ended up on the ground with a broken leg.
Donna: I didn't let that stop me, though. 
You've seen me playing cards, it takes a lot more than one attempt to make me give up. 
As soon as that cast was off I got to work. 
Started gymnastics, went to trapeze lessons, eventually even made it to the trapeze academy. 
And after I graduated, well, obviously I ended up here. 
I practice trapeze and perform all the time, knowing that I won't- no, can't fall. 
I have faith in my own skills, and the audience has faith in me. 
That's what let's me overcome my fear. 
But you introduced another element. 
It's... hard to admit, but I've been dodging the idea of performing alongside you. 
When you first arrived, I could tell that you had talent. 
But I could also tell that you had a lot to learn. 
I was scared that if I tried to perform with you, something would go wrong. 
You might not catch me, or I might not catch you, and somehow we would end up falling and I wouldn't have the ability to stop it.
That fear of mine has been holding both of us back, but I don't want to let it anymore.
I'd like to think that I've gotten to know you pretty well, and I now have faith in both you and your skills. 
Would... 
Would you be willing to perform with me?
->Yes
	Donna: Perfect. I'm sorry it took me so long.
->Can I trust you to not let me fall?
	Donna: Yes, if I can trust you to do the same.
->Are you sure you're comfortable with this?
	Donna: To be honest I'm not sure, but I think it's about time either way. 
	I'm ready if you are.
Donna: The next time you practice trapeze, I'll go over the basics of performing together and start teaching you some collaborative tricks. 
If we get our act down well enough, we might even be able to debut our team performance at the next show. 
Thanks again for being patient with me, and for being willing to work together. 
This time I truly mean it: welcome to the team.  
<<ActivateDuoTutorial GameManager>>
===

title: DonnaBlackjack
tags: 
---
<<ShowImage TextManager DonnaBlackJack>>
Donna: Alright, hit me.
Ah, darn it. Already over 21. You win this round.
Hmm... I think I'll stand here, got a good feeling about this hand.
...
Ah, not even close. This round's yours too.
But I'm winning this next one.
...Some time later...
Donna: Man, can't believe I didn't even win a single round.  
Still, that was a fun. Look forward to playing against you again.
<<IncreaseTrust PlayerAvatar 5>>
<<HideImage TextManager>>
===

title: DonnaTest
tags: 
---
<<if visited("DonnaTest") is false>>
    Donna: Testing!
	-> Was the test successful?
	 Donna: Seems to be!
	-> What are you testing?
	 Donna: Yarn spinner in general, and also whether or not branching paths works.
	Donna: Overall I'd say it is working well.
<<else>>
    Donna: Hey!! 
<<endif>>
===

title: DonnaInitConversation
tags: 
---
What do you want to do?
->Talk to Donna
	<<DAttemptTalk Donna>>
->Hang out with Donna 
	<<if $DonnaInvitable is true>>
		Donna: Sure, it'd be fun to hang out with you.
		<<DFollowPlayer Donna true>>
	<<else>>
		Donna: Sorry, I'm busy right now.
	<<endif>>
===

title: DonnaFollowing
tags: 
---
Stop hanging out with Donna?
->Yes
	Donna: It's been fun!
	<<DFollowPlayer Donna false>>
->No
===

title: DonnaTrapezePractice0
tags: trapeze_practice
---
Donna: Oh hey [$PlayerName], are ya here to get some practice in too?
->Yes
	Donna: Sweet, good chance for me to see how your technique's improved. 
->No
	Donna: Eh, that's alright. It's good to take things at your own pace, just gotta make sure you practice enough that ya don't get rusty. 
	I'm pretty much here all the time practicing myself, it's become my morning ritual.
===

title: DonnaSolitaire
tags: 
---
Donna: Ugh, you ever just have one of those nights? 
I've been playing solitaire for hours now, but can't seem to win a single round. 
Somehow managed to deal three games in a row where there weren't even any moves I could make!
->Sounds like bad luck.
	<<IncreaseTrust PlayerAvatar 2>>
	Donna: Heh, Lady Luck certainly does have it out for me. 
	Doesn't help that I'm legendarily bad at these games. 
	If I actually wagered money on poker, my account would be in the red before the hand was dealt. 
	Still, I love playing cards, doesn't matter what the game or how hard I lose it.
->Maybe you should quit while you're behind.
	<<DecreaseTrust PlayerAvatar 5>>
	Donna: Nah, no one ever gets anywhere in life with that attitude. 
	I actually really like playing all these card games, even though you can probably count the number of times I've won on one hand. 
	But there's no chance of winning one if I don't play. Besides, how can I complain about losing if I don't lose?
->Say Nothing
	Donna: Oh well, I'm sure the next round'll go better.
===

title: DonnaNewDeck
tags: 
---
Donna: I got a new set of playing cards today. I always make sure to check for themed decks I don't have every time the shop gets a shipment.
===

title: DonnaAdvice1
tags: 
---
<<if visited("DonnaAdvice1") is false>>
	<<set $DonnaAdviceChosen to "">>
	<<set $DACbad to false>>
	Donna: Hmm... It still feels too stale... 
	Oh! 
	I almost didn't see you there [$PlayerName]! 
	But you have good timing, I could really use yer advice. 
	I've been practicing my routine for the next big show, and I've been tryin' to come up with a trick that might help freshen things up. 
	Right now the set is jump, shooting star, and then a front flip. 
	I want to add one more trick before I catch the bar, do ya have any suggestions?
	->Forward-Over
		<<set $DonnaAdviceChosen to "Forward-Over">>
	->Cutaway Half
		<<set $DonnaAdviceChosen to "Cutaway Half">>
	->Castaway
		<<set $DonnaAdviceChosen to "Castaway">>
	Donna: Sounds worth a try, thanks for the advice.
<<else>>
	[[DonnaAdvice2]]
<<endif>>
===

title: DonnaAdvice2
tags: 
---
<<if visited("DonnaAdvice1") is false>>
	[[DonnaAdvice1]]
<<elseif $DonnaAdviceChosen eq "Castaway">>
	Donna: I tried out the Castaway catch at the end of my set like you recommended, but it didn't work out. 
	It needed the opposite momentum I had from the frontflip. Kinda kicking myself for not realizing that before I tried it. 
	I'd recommend spending some more time practicing, I think we both need to brush up on our tricks. 
	<<if visited("DonnaAdvice2") is false>>
		<<DecreaseTrust PlayerAvatar 10>>
	<<endif>>
<<else>>
	Donna: Hey [$PlayerName]! Thanks again for the advice you gave me, the [$DonnaAdviceChosen] worked like a charm. 
	Really spiced up that part of the routine.
	<<if visited("DonnaAdvice2") is false>>
		<<IncreaseTrust PlayerAvatar 10>>
	<<endif>>
<<endif>>
===

title: DonnaArchery
tags: 
---
Donna: Ugh, today's been rough. 
I need to head to the archery range and take out a few targets, that always helps me feel better.
===

title: DonnaKaroake
tags: 
---
Donna: There's nothing more fun than some good ol' fashion karaoke. 
I always get super into the zone, there's nothing quite like singing the night away.
===

title: DonnaPerformanceDay
tags: 
---
Is this line showing up? Weird.
Donna: Today's the big show! Are you ready to show off your trapeze skills to everyone in the audience?
->Yes
	Donna: Awesome
->No
	Donna: Uh oh
<<ChangeSceneTrapeze GameManager>>
===